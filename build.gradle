plugins {
    id "fabric-loom" version "$loom_version"
    id "io.github.juuxel.loom-vineflower" version "$loom_vineflower_version"
    id 'org.ajoberstar.grgit' version '5.2.0'
}

version = "${getVersionMetadata()}"

base {
    archivesName = archives_name
}

repositories {
    maven { url 'https://jitpack.io' }
    flatDir { dirs "libs" }
}

loom {
    accessWidenerPath = file("src/main/resources/worldpreview.accesswidener")
}

dependencies {
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings "com.github.RedLime:yarn:${minecraft_version}.build.$yarn_build:v2"
    modImplementation "net.fabricmc:fabric-loader:$loader_version"
    modImplementation "xyz.tildejustin:state-output-common:1.0.0+1.3-1.20"
    include(modImplementation fabricApi.module("fabric-key-binding-api-v1", "$fabric_api_version-$minecraft_version"))
}

processResources {
    inputs.property "version", version

    filesMatching("fabric.mod.json") {
        expand "version": version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(8)
}

java {
    // withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}" }
    }
}

vineflower {
    toolVersion = vineflower_version
}

@SuppressWarnings(["GrMethodMayBeStatic", "GrUnresolvedAccess"])
def getVersionMetadata() {
    // CI build version numbers
    def build_id = System.getenv("RELEASE_TAG")
    if (build_id != null) return build_id

    // development builds
    if (grgit == null) return "dev"

    // named development builds
    def id = grgit.head().getAbbreviatedId()
    if (!grgit.status().clean) id += "-dirty"

    return "rev.$id"
}
